DEFAULT_NAME_PREFIX = "Durrrrr"
MAX_NAME_LENGTH = 64
MAX_MESSAGE_LENGTH = 512

$ !->
  # set the name and message max lengths
  $ \#name .attr \maxlength MAX_NAME_LENGTH
  $ \#message .attr \maxlength MAX_MESSAGE_LENGTH

  # generate a random default username
  name = "#DEFAULT_NAME_PREFIX#{Math.floor 10000 * Math.random ()}"
  $ \#name .val name

  # prompt a user for a new name
  promptName = !->
    tName = prompt "Choose a name" name
    if tName is not null
      if tName.length > MAX_NAME_LENGTH
        alert "Your name can't be longer than #MAX_NAME_LENGTH characters"
      else
        name := tName

  # add a new message to the ui
  even = true
  addMessage = !(msg)->
    $ \#messages .prepend ($ '<div class="message">'
      .append ($ '<span class="time">' .text "[#{new Date msg.time .format \hh:mm:ss}]")
      .append ($ '<span class="name">' .text "<#{msg.name}>")
      .append ($ '<span class="message">' .text msg.message)
      .addClass (if even then \even else \odd))
    even := not even

  # get a socket.io socket
  sock = io.connect \http://127.0.0.1:8888

  # socket event handlers
  handlers =
    backlog: !(backlog)->
      for msg of backlog
        addMessage msg

    message: !(msg)->
      addMessage msg

  # add socket event handlers
  for msg in handlers
    sock.on msg, handlers[msg]

  # change name
  $ \#name .click !->
    promptName ()

  # send messages
  $ \#message-form .submit !(ev)->
    # don't actually submit the form
    ev.preventDefault ()

    msgInput = $ \#message
    msg = msgInput .val ()
    msgInput.val ""

    if msg != ""
      sock.emit \message name: name, message: msg

  # ask the user to pick a name
  promptName ()
